plugins {
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id "io.freefair.lombok" version "4.1.6"
    id "org.flywaydb.flyway" version "6.2.2"
    id "org.openapi.generator" version "4.2.2"
    id 'java'
}

group 'com.peltops'
version '1.0-SNAPSHOT'
sourceCompatibility = 11

repositories {
    mavenCentral()
}

task validateTS29571_CommonData(type: org.openapitools.generator.gradle.plugin.tasks.ValidateTask) {
    inputSpec = "$rootDir/spec/TS29571_CommonData.yaml".toString()
}

task validateTS29511_N5g(type: org.openapitools.generator.gradle.plugin.tasks.ValidateTask) {
    inputSpec = "$rootDir/spec/TS29511_N5g-eir_EquipmentIdentityCheck.yaml".toString()
}

task validateSpecs(dependsOn: ['validateTS29571_CommonData', 'validateTS29511_N5g'])

openApiValidate {
    inputSpec = "$rootDir/spec/TS29511_N5g-eir_EquipmentIdentityCheck.yaml".toString()
}

openApiGenerate {
    verbose = true
    generatorName = "spring"
    inputSpec = "$rootDir/spec/TS29511_N5g-eir_EquipmentIdentityCheck.yaml".toString()
    outputDir = "$buildDir/generated/openapi".toString()
    apiPackage = "com.peltops.nestling.api"
    invokerPackage = "com.peltops.nestling"
    modelPackage = "com.peltops.nestling.model"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'org.postgresql:postgresql'
    compile('org.flywaydb:flyway-core')
    implementation 'org.openapitools:jackson-databind-nullable:0.1.0'
    implementation 'io.springfox:springfox-swagger2:2.8.0'
    implementation 'io.springfox:springfox-swagger-ui:2.8.0'
    implementation 'javax.xml.bind:jaxb-api:2.2.11'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation('org.flywaydb.flyway-test-extensions:flyway-spring-test:6.1.0')
}

test {
    useJUnitPlatform()
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/nestling'
    user = 'nestling'
    password = ''
    schemas = ['public']
    locations = ["filesystem:$project.projectDir/src/main/resources/db/migration"]
}

test.dependsOn(flywayClean,flywayMigrate)